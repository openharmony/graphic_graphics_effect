# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphics_effect/config.gni")

config("export_config") {
  include_dirs = [
    "include",
    "include/core",
    "include/pipeline",
    "include/hps",
    "include/ext",
    "include/effect",
    "include/effect/filter",
    "include/effect/shader",
    "include/effect/mask",
    "include/effect/shape",
    "include/util"
  ]
  cflags_cc = [ "-std=c++17" ]
}

ohos_source_set("graphics_effect_src") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  cflags = [
    "-Wall",
    "-O2",
    "-ftrapv",
    "-FPIC",
    "-FS",
    "-D_FORTIFY_SOURCE=2",
    "-fvisibility=hidden",
    "-fdata-sections",
    "-ffunction-sections",
  ]
  cflags_cc = [
    "-std=c++17",
    "-O2",
    "-ftrapv",
    "-FPIC",
    "-FS",
    "-D_FORTIFY_SOURCE=2",
    "-fdata-sections",
    "-ffunction-sections",
    "-fvisibility=hidden",
    "-fvisibility-inlines-hidden",
  ]

  public_configs = [ ":export_config" ]
  include_dirs = [
    "include",
    "include/core",
    "include/pipeline",
    "include/hps",
    "include/ext",
    "include/effect",
    "include/effect/filter",
    "include/effect/shader",
    "include/effect/mask",
    "include/effect/shape",
    "include/util"
  ]

  sources = [
    "src/core/ge_visual_effect.cpp",
    "src/core/ge_visual_effect_container.cpp",
    "src/core/ge_visual_effect_impl.cpp",
    "src/pipeline/ge_render.cpp",
    "src/pipeline/ge_filter_composer.cpp",
    "src/pipeline/ge_direct_draw_on_canvas_pass.cpp",
    "src/pipeline/ge_mesa_fusion_pass.cpp",
    "src/pipeline/ge_hps_build_pass.cpp",
    "src/pipeline/ge_hps_upscale_pass.cpp",
    "src/hps/ge_hps_effect_filter.cpp",
    "src/effect/filter/ge_shader_filter.cpp",
    "src/effect/filter/ge_aibar_shader_filter.cpp",
    "src/effect/filter/ge_bezier_warp_shader_filter.cpp",
    "src/effect/filter/ge_color_gradient_shader_filter.cpp",
    "src/effect/filter/ge_content_light_shader_filter.cpp",
    "src/effect/filter/ge_direction_light_shader_filter.cpp",
    "src/effect/filter/ge_dispersion_shader_filter.cpp",
    "src/effect/filter/ge_displacement_distort_shader_filter.cpp",
    "src/effect/filter/ge_edge_light_shader_filter.cpp",
    "src/effect/filter/ge_frosted_glass_blur_shader_filter.cpp",
    "src/effect/filter/ge_frosted_glass_shader_filter.cpp",
    "src/effect/filter/ge_gamma_correction_filter.cpp",
    "src/effect/filter/ge_grey_shader_filter.cpp",
    "src/effect/filter/ge_grid_warp_shader_filter.cpp",
    "src/effect/filter/ge_kawase_blur_shader_filter.cpp",
    "src/effect/filter/ge_linear_gradient_blur_shader_filter.cpp",
    "src/effect/filter/ge_magnifier_shader_filter.cpp",
    "src/effect/filter/ge_map_color_by_brightness_shader_filter.cpp",
    "src/effect/filter/ge_mask_transition_shader_filter.cpp",
    "src/effect/filter/ge_mesa_blur_shader_filter.cpp",
    "src/effect/filter/ge_sound_wave_filter.cpp",
    "src/effect/filter/ge_sdf_edge_light.cpp",
    "src/effect/filter/ge_sdf_from_image_filter.cpp",
    "src/effect/filter/ge_variable_radius_blur_shader_filter.cpp",
    "src/effect/filter/ge_water_droplet_transition_filter.cpp",
    "src/effect/filter/ge_water_ripple_filter.cpp",
    "src/effect/shader/ge_shader.cpp",
    "src/effect/shader/ge_aurora_noise_shader.cpp",
    "src/effect/shader/ge_border_light_shader.cpp",
    "src/effect/shader/ge_circle_flowlight_effect.cpp",
    "src/effect/shader/ge_color_gradient_effect.cpp",
    "src/effect/shader/ge_contour_diagonal_flow_light_shader.cpp",
    "src/effect/shader/ge_frosted_glass_effect.cpp",
    "src/effect/shader/ge_particle_circular_halo_shader.cpp",
    "src/effect/shader/ge_wavy_ripple_light_shader.cpp",
    "src/effect/shader/gex_dot_matrix_shader.cpp",
    "src/effect/shader/gex_flow_light_sweep_shader.cpp",
    "src/effect/shader/gex_complex_shader.cpp",
    "src/effect/mask/ge_double_ripple_shader_mask.cpp",
    "src/effect/mask/ge_frame_gradient_shader_mask.cpp",
    "src/effect/mask/ge_image_shader_mask.cpp",
    "src/effect/mask/ge_linear_gradient_shader_mask.cpp",
    "src/effect/mask/ge_pixel_map_shader_mask.cpp",
    "src/effect/mask/ge_radial_gradient_shader_mask.cpp",
    "src/effect/mask/ge_ripple_shader_mask.cpp",
    "src/effect/mask/ge_use_effect_shader_mask.cpp",
    "src/effect/mask/ge_wave_gradient_shader_mask.cpp",
    "src/effect/mask/ge_wave_disturb_shader_mask.cpp",
    "src/effect/shape/ge_sdf_shader_shape.cpp",
    "src/effect/shape/ge_sdf_rrect_shader_shape.cpp",
    "src/effect/shape/ge_sdf_pixelmap_shader_shape.cpp",
    "src/effect/shape/ge_sdf_transform_shader_shape.cpp",
    "src/effect/shape/ge_sdf_union_op_shader_shape.cpp",
    "src/effect/shape/ge_sdf_border_shader.cpp",
    "src/effect/shape/ge_sdf_clip_shader.cpp",
    "src/effect/shape/ge_sdf_color_shader.cpp",
    "src/effect/shape/ge_sdf_shadow_shader.cpp",
    "src/util/ge_system_properties.cpp",
    "src/util/ge_tone_mapping_helper.cpp",
    "src/ext/ge_external_dynamic_loader.cpp",
    "src/ext/gex_marshalling_helper.cpp",
  ]

  deps = [ ":utils" ]

  external_deps = []
  if (is_arkui_x) {
    deps += [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${target_os}",
      "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics:2d_graphics",
      "//third_party/bounds_checking_function:libsec_static",
      "//third_party/skia:skia_$target_os",
    ]
    external_deps += [ "hilog:libhilog" ]
  } else {
    external_deps += [
      "bounds_checking_function:libsec_shared",
      "graphic_2d:2d_graphics",
      "hilog:libhilog",
    ]
  }

  if (!build_ohos_sdk) {
    external_deps += [ "hitrace:hitrace_meter" ]
  }

  defines = []
  if (current_os == "ohos") {
    defines += [ "GE_OHOS" ]
    external_deps += [ "init:libbegetutil" ]
  }
  if (is_arkui_x) {
    if (graphics_effect_feature_upgrade_skia) {
      defines += [ "USE_M133_SKIA" ]
    }
  }
  if (ge_is_ohos || ge_is_linux) {
    defines += [ "GE_PLATFORM_UNIX" ]
  }

  output_name = "graphics_effect"
  part_name = "graphics_effect"
  subsystem_name = "graphic"
}

if (is_arkui_x) {
  ohos_source_set("libgraphics_effect") {
    branch_protector_ret = "pac_ret"
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }

    deps = [
      ":graphics_effect_src",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${target_os}",
      "//third_party/bounds_checking_function:libsec_static",
    ]

    part_name = "graphics_effect"
    subsystem_name = "graphic"
  }
} else {
  ohos_shared_library("graphics_effect_core") {
    branch_protector_ret = "pac_ret"
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }

    public_configs = [ ":export_config" ]

    deps = [ ":graphics_effect_src" ]
    external_deps = [
      "bounds_checking_function:libsec_shared",
      "hilog:libhilog",
    ]

    output_name = "graphics_effect"
    part_name = "graphics_effect"
    subsystem_name = "graphic"
  }
}

# utils {{{
group("utils") {
  if (current_os == "ohos") {
    public_external_deps = [ "c_utils:utils" ]
  } else if (current_os == "android" || current_os == "ios") {
    public_external_deps = [ "c_utils:utilsbase" ]
  } else {
    public_deps = [ ":mock_utils" ]
  }
}

config("utils_config") {
  include_dirs = [ "src/util/mock" ]
}

ohos_source_set("mock_utils") {
  public_configs = [ ":utils_config" ]
  sources = [
    "src/util/mock/parcel.cpp",
    "src/util/mock/refbase.cpp",
  ]
  public_external_deps = [ "bounds_checking_function:libsec_shared" ]
  part_name = "graphics_effect"
  subsystem_name = "graphic"
}
# utils }}}
